sonarqube {
    properties {
        property "sonar.projectName", "de.qaware.chronix:chronix-server-query-language"
    }
}

sourceSets {
    jmh {
        java.srcDirs = ['src/test/java']
        resources.srcDirs = ['src/test/resources']
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

task jmhCQLBenchmark(type: JavaExec, dependsOn: jmhClasses) {
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)

dependencies {
    compile group: 'org.antlr', name: 'antlr4', version: '4.5.1-1'
    compile project(':chronix-server-plugin-management')

    testCompile project(':chronix-server-function-metric')
    testCompile project(':chronix-server-type-metric')

    testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.21'
    testCompile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.21'
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {

    inceptionYear '2015'

    scm {
        connection "scm:git:${project.scmUrl}"
        developerConnection "scm:git:${project.scmUrl}"
        url project.websiteUrl
    }

    issueManagement {
        system 'GitHub'
        url project.issueTrackerUrl
    }

    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    organisation {
        name 'QAware GmbH'
        url 'https://www.qaware.de'
    }

    developers {
        developer {
            id 'florianlautenschlager'
            name 'Florian Lautenschlager'
            email 'florian.lautenschlager@qaware.de'
            organization 'QAware GmbH'
            organizationUrl 'https://www.qaware.de'
            roles { role 'Developer' }
        }
    }
}

publishing {
    publications {
        chronixServerQueryLanguage(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', project.displayName)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', project.websiteUrl)

                asNode().children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['chronixServerQueryLanguage']
    dryRun = false
    publish = true
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['Apache-2.0']
        labels = ['java', 'time series', 'chronix', 'server']
        websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            vcsTag = "v${project.version}"
            attributes = [
                    'chronix-server-query-language': ['de.qaware.chronix', project.group, project.name].join(':')
            ]
            mavenCentralSync {
                sync = false
            }
        }
    }
}